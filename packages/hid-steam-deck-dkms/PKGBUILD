pkgname=hid-steam-deck-dkms
_pkgbase=hid-steam
pkgver=4.1
pkgrel=1
pkgdesc="hid-steam Steam Deck Controller support (DKMS)"
arch=('x86_64')
url='https://gitlab.com/evlaV/linux-integration'
_commiturl="$url/-/raw/3d6a63bb67fc8c4df6b51a9e582098ce8fef3348"

depends=('dkms')

source=("${_commiturl}/drivers/hid/hid-ids.h"
        "${_commiturl}/drivers/hid/hid-steam.c"
        "${_commiturl}/drivers/hid/Kconfig"
        'Kbuild'
        'dkms.conf'
        'ff.patch')
sha256sums=('6959af584e1461d48685303c1346d273f9ba1b5dce0e3caba8ae5d463039d477'
            '3c32d3eedef12a81372a40943511919378f219388a4e8d6420cf9ed47a596b62'
            '42fba70a3372c1723834d13f9b1e10daab2f9f4b0e9771fd3d2acad70467fd95'
            '9ed2dcb2e909a7c410766ea9cbd1c8eaa1090c84050014a0d4e27db53749aa83'
            '9c08f39b3cd67ee688ea54d1e5cf913efbe366c506f0105065746131633ab454'
            'daff7ec7a3b95184c440d97ac530afd405e96a1db8cbcb49896791884d2600f8')

package() {
    local dkmsdir="${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    install -Dm644 'hid-ids.h' 'hid-steam.c' 'Kconfig' 'Kbuild' 'dkms.conf' -t "${dkmsdir}"
    patch -d "${dkmsdir}" -Np3 -i "${srcdir}/ff.patch"
    sed -e "s/@_PKGBASE@/${_pkgbase}/" \
        -e "s/@PKGVER@/${pkgver}/" \
        -i "${dkmsdir}/dkms.conf"
}

