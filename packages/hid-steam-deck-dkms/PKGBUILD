pkgname=hid-steam-deck-dkms
_pkgbase=hid-steam
pkgver=4.3
pkgrel=1
pkgdesc="hid-steam Steam Deck Controller support (DKMS)"
arch=('x86_64')
url='https://gitlab.com/evlaV/linux-integration'
_commiturl="$url/-/raw/3d6a63bb67fc8c4df6b51a9e582098ce8fef3348"

depends=('dkms')

source=("${_commiturl}/drivers/hid/hid-ids.h"
        "${_commiturl}/drivers/hid/hid-steam.c"
        'Kbuild'
        'dkms.conf')
sha256sums=('6959af584e1461d48685303c1346d273f9ba1b5dce0e3caba8ae5d463039d477'
            '3c32d3eedef12a81372a40943511919378f219388a4e8d6420cf9ed47a596b62'
            '82bd54d3f1953992c49d3862cdc7efa2801155ecb3e6b5562471d9cf998f69a4'
            '9c08f39b3cd67ee688ea54d1e5cf913efbe366c506f0105065746131633ab454')

package() {
    local dkmsdir="${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    install -Dm644 'hid-ids.h' 'hid-steam.c' 'Kbuild' 'dkms.conf' -t "${dkmsdir}"
    sed -e "s/@_PKGBASE@/${_pkgbase}/" \
        -e "s/@PKGVER@/${pkgver}/" \
        -i "${dkmsdir}/dkms.conf"
}

