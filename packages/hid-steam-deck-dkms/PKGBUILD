pkgname=hid-steam-deck-dkms
_pkgbase=hid-steam
pkgver=4
pkgrel=2
pkgdesc="hid-steam Steam Deck Controller support (DKMS)"
arch=('x86_64')
url='https://gitlab.com/evlaV/linux-integration'
_branchurl="$url/-/raw/6.1/cristicc/wip-integration/"

depends=('dkms')

source=("${_branchurl}/drivers/hid/hid-ids.h"
        "${_branchurl}/drivers/hid/hid-steam.c"
        "${_branchurl}/drivers/hid/Kconfig"
        'dkms.conf'
        'Kbuild'
        'ff.patch')
sha256sums=('96b46aca1f55a7fe1f1a167356c3e014199928c1735c25cc2194626646c1c7bd'
            '3c32d3eedef12a81372a40943511919378f219388a4e8d6420cf9ed47a596b62'
            '42fba70a3372c1723834d13f9b1e10daab2f9f4b0e9771fd3d2acad70467fd95'
            '9c08f39b3cd67ee688ea54d1e5cf913efbe366c506f0105065746131633ab454'
            '9ed2dcb2e909a7c410766ea9cbd1c8eaa1090c84050014a0d4e27db53749aa83'
            'daff7ec7a3b95184c440d97ac530afd405e96a1db8cbcb49896791884d2600f8')

prepare() {
    for patchfile in *.patch; do
        patch -d .. -Np3 -i "$patchfile"
    done
}

package() {
    local dest="${pkgdir}/usr/src/${_pkgbase}-${pkgver}"
    install -Dm644 * -t "$dest"
    sed -e "s/@_PKGBASE@/${_pkgbase}/" \
        -e "s/@PKGVER@/${pkgver}/" \
        -i "$dest/dkms.conf"
}

